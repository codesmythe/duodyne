Name     STAT188 ;
PartNo   18801 ;
Date     12/02/2019 ;
Revision 01 ;
Designer JRCoffman ;
Company  Coffman Consulting ;
Assembly SBC188b ;
Location  ;
Device   G22V10 ;

/* Updated 9/14/2024 for DUODYNE.(DWERNER)  Note that MREQ could be simplified -- Future work here */

/* *************** INPUT PINS *********************/
PIN  1  =  ALE                    ; /* ADDRESS LATCH ENABLE            */
PIN  2  =  ST2                    ; /*                                 */
PIN  3  =  ST1                    ; /* THREE STATUS BITS               */
PIN  4  =  ST0                    ; /*                                 */
PIN  5  =  A19                    ; /* HIGH ORDER ADDRESS BIT          */
PIN  6  = !UCS                    ; /* UPPER CHIP SELECT == !CS_ROM    */
PIN  7  = !MCS0                   ; /*                                 */
PIN  8  = !MCS1                   ; /* FOUR MIDDLE CHIP SELECTS        */
PIN  9  = !MCS2                   ; /*                                 */
PIN 10  = !MCS3                   ; /*                                 */
PIN 11  = !PCS0                   ; /*                                 */
PIN 13  =  BUSAK                  ; /* HLDA OUT FROM CPU               */
PIN 21  =  BDREQ                  ; /* BUS DMA REQUEST                 */
PIN 23  = !PCS1                   ; /*                                 */
/*PIN     =                         ; /*                                 */
/*PIN     =                         ; /*                                 */

/* *************** OUTPUT PINS *********************/
PIN 14  = !CS0                    ; /*                                 */
PIN 15  = !CS1                    ; /*                                 */
PIN 16  = !M1                     ; /*                                 */
PIN 17  = !MREQ_IO                ; /* INPUT/OUTPUT MAY TRI-STATE      */
PIN 18  =  HALT                   ; /* SIGNAL HALT CONDITION           */
PIN 19  = !IORQ                   ; /*                                 */
PIN 20  = !G_DATA                 ; /* LS245 HANDLING DATA LINES TO BUS */
PIN 22  =  !BUS_DREQ               ; /* BUS DMA REQUEST                 */
/*PIN     =                         ; /*                                 */




MEMOP	=	 ST2 & (!ST1 # !ST0) ;
IDLE	=	 ST2 &  ST1  &  ST0  ;

HALT	=      !ST2 &  ST1  &  ST0  ;
IO_OP	=	!ST2 & (!ST1 # !ST0) ;	/* INCLUDES -INTA- */
INTA	=	!ST2 & !ST1 & !ST0 ;

MCS	=	MCS0 # MCS1 # MCS2 # MCS3 ;
LCLMREQ =	!ALE & MEMOP ;
EXTMREQ =	!ALE &  MCS & MEMOP & !BUSAK ;
EXTIORQ =	!ALE & (PCS0 # PCS1) & !BUSAK  #
		!ALE & INTA & !BUSAK ;

CSMEM	=	LCLMREQ & !BUSAK  #  BUSAK & MREQ_IO ;

/* *************** OUTPUT PINS *********************/
BUS_DREQ =	BDREQ ;	/* SIMPLY INVERT */

CS0	=	!A19 & CSMEM ;
CS1	=	 A19 & CSMEM & !UCS & !MCS ;
G_DATA =	 BUSAK # MREQ_IO # EXTIORQ  ;

IORQ 		= EXTIORQ ;
IORQ.OE		= !BUSAK ;
MREQ_IO		= (EXTMREQ # CS0 # CS1) & UCS  ;
MREQ_IO.OE	= !BUSAK ;

M1	=	!ST1 & !ST0 ;		/* INTA AND INSTR. FETCH */
